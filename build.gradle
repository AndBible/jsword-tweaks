apply plugin: "java"
sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    jsword_project = "../../jsword"
    jsword_src = "${jsword_project}/src/main/java"
    jsword_resources = "${jsword_project}/src/main/resources"
    tweaks_jsword_src = "src/main/java/jsword"
    tweaks_jsword_resources = "src/main/resources"
    tweaks_util_src = "src/util/java"
    build_src = "src/jsword/java"
    build_resources = "src/jsword/resources"
    distribution = "distribution"
    andbible = "../and-bible"
    build_class = "build"
    jswordtweaks_home = "$rootDir"
}

task clean_project {
    doFirst {
        delete("${build_src}", "${distribution}")
    }
}

task init() {
    doFirst {
        mkdir build_src
        mkdir build_resources
        mkdir distribution
    }
}

task mergeResources(type: Copy, dependsOn: init) {
    from("${jsword_resources}") {
        exclude("org/crosswire/jsword/versification/**")
    }
    from("${jsword_resources}") {
        include("org/crosswire/jsword/versification/**")
        filter({String line -> if (line.contains('!zerosUnmapped')) '#!zerosUnmapped // commented out' else line})
    }
    // switch the Chinese localisation defaults around so default CN is simplified and TW is Traditional
    from("${jsword_resources}") {
        include("BibleNames_zh*.properties")
        rename("BibleNames_zh.properties", "BibleNames_zh_TW.properties")
        rename("BibleNames_zh_CN.properties", "BibleNames_zh.properties")
    }
    into(build_resources)
}

task mergeCode(type: Copy, dependsOn: init) {
    from("${jsword_src}") {
        exclude("org/crosswire/jsword/book/install/sword/FTPExample.java")
		exclude("org/crosswire/jsword/examples/Bench.java")
    }
    into(build_src)
}


sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/jsword/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/jsword/resources'
        }
        output.resourcesDir = "build/classes/main"
    }
}


clean.dependsOn clean_project, init
build.dependsOn mergeResources, mergeCode, compileJava, jar


repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.httpcomponents:httpcore:4.3.2"
    implementation "org.apache.httpcomponents:httpclient:4.3.2"
    implementation "javatar:javatar:2.5"
    implementation "org.apache.commons:commons-compress:1.12"
    implementation "commons-logging:commons-logging:1.1.3"
    implementation "commons-lang:commons-lang:2.5"
    implementation "com.chenlb.mmseg4j:mmseg4j-all:1.8.6"
    implementation "org.jdom:jdom2:2.0.5"
    implementation "org.apache.lucene:lucene-core:3.0.3"
    implementation "org.apache.lucene:lucene-analyzers:3.0.3"
    implementation "org.apache.lucene:lucene-smartcn:3.0.3"
    implementation "org.apache.lucene:lucene-snowball:3.0.3"
	implementation "org.slf4j:slf4j-api:1.7.6"
	implementation "org.slf4j:slf4j-jdk14:1.7.6"
    testImplementation "junit:junit:4.11"
}